apply plugin: 'maven-publish'
apply plugin: 'signing'

//simple settings
project.afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {

                setGroupId GROUP
                setArtifactId POM_ARTIFACT_ID
                version VERSION_NAME

                from components.java
                artifact javadocJar
                artifact sourcesJar

                pom {
                    name = POM_NAME
                    packaging = POM_PACKAGING
                    description = POM_DESCRIPTION
                    url = POM_URL

                    organization {
                        name = POM_ORG_NAME
                        url = POM_ORG_URL
                    }

                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name = POM_LICENSE_NAME
                            url = POM_LICENSE_URL
                            distribution = POM_LICENSE_DIST
                        }
                    }

                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            email = POM_DEVELOPER_EMAIL
                        }

                        developer {
                            id = POM_DEVELOPER_ID_TD
                            name = POM_DEVELOPER_NAME_TD
                            email = POM_DEVELOPER_EMAIL_TD
                        }
                    }
                }

                repositories {
                    maven {
                        def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                        def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                        credentials {
                            username findProperty('sonatypeUsername')
                            password findProperty('sonatypePassword')
                        }
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
